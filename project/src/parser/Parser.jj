options {
  STATIC=false;
}

PARSER_BEGIN(Parser)

package parser;
import ast.*;
import ast.arithmetic.*;
import ast.control_flow.*;
import ast.identifiers.*;
import ast.logical.*;
import ast.references.*;
import ast.tuples.*;
import ast.string.*;
import ast.print.*;
import java.util.ArrayList;
import java.util.List;
import java.util.Map;
import java.util.HashMap;

public class Parser { }

PARSER_END(Parser)

SKIP :
{
  " "
| "\t"
| "\r"
}

TOKEN : /* OPERATORS */
{
  < Num: (["0"-"9"]) + >
| < PLUS : "+" >
| < TIMES : "*" >
| < MINUS : "-">
| < NEG : "~">
| < GT: ">" >
| < LT: "<" >
| < GTOEQ: ">=" >
| < LTOEQ: "<=" >
| < DER: "!">
| < DIV : "/">
| < LPAR : "(" >
| < RPAR : ")" >
| < TRUE : "true" >
| < FALSE : "false" >
| < EQ : "=" >
| < NEQ : "!=" >
| < AND : "&&" >
| < OR : "||" >
| < IFQ : "?" >
| < COLON : ":" >
| < SEMICOLON : ";" >
| < WHILE : "while" >
| < DO : "do" >
| < END : "end" >
| < NEW : "new" >
| < ASSIGN : ":=" >
| < UNIT : "()" >
| < EOL: "\n" >
| < LET: "let" >
| < IN: "in" >
| < IF: "if" >
| < THEN: "then" >
| < ELSE: "else" >
| < FUN: "fun" >
| < ARROW: "->" >
| < LBRAC: "{" >
| < RBRAC: "}" >
| < COMMA: "," >
| < FIRST: "first" >
| < SECOND: "second" >
| < LAST: "last" >
| < MATCH: "match" >
| < WITH: "with">
| < QUOTE: "\"">
| < CONCAT: "concat">
| < PRINT: "print">
| < PRINTLN: "println">
| < SPLIT: "split">
}

TOKEN :
{
  < INT : (< DIGIT >)+ >
| < #DIGIT : [ "0"-"9" ] >
| < ID : (< LETTER >)+ >
| < #LETTER : [ "a"-"z", "A"-"Z" ] >
| < #LETTER_SPECIAL : [ "a"-"z", "A"-"Z", " ", "0"-"9", "!", "@", "#", "$", "%", "&", "/", "(", ")", "=", "?", "*",
    "-", "+", "_", "{", "[", "]", "}", ";", ":"] >
| < STRING : <QUOTE> (< LETTER_SPECIAL >)+ <QUOTE> >
}

Exp Start() :
{ Exp e; }
{
  e = decl() <EOL>  { return e; }
  | <EOF> { throw new ParseException("End of File encountered. Stopping Parsing."); }
}

/*Exp sequence() :
 { Exp e1, e2; }
 {
   e1 = decl() ( <SEMICOLON> e2 = sequence() { e1 = new ASTSeq(e1, e2); } )* { return e1; }
 }*/

Exp decl() :
{ Exp e, b; List<ASTParameter> params = new ArrayList<ASTParameter>();
 Map<String, Exp> variables = new HashMap<String, Exp>(); Token x, id, type; }
{
  <FUN> ( <LPAR> id = <ID> <COLON> type = <ID> <RPAR> { params.add(new ASTParameter(id.image, type.image)); } )+
    <ARROW> e = decl() { return new ASTClosure(params, e); }
 |  <LET> ( x = < ID > < EQ > e = decl() { variables.put(x.image, e); } )+
        <IN > b = decl() { return new ASTLet(variables, b); }
 |  e = control_flow() { return e; }
}

Exp control_flow() :
{ Exp e,b,eb, pair, match; Token id, d, x;
 List<String> ids = new ArrayList<String>();}
{
  <IF> e = decl() <THEN> b = decl() [<ELSE> eb = decl() <END>  { return new ASTElse(e, b, eb); }]
      <END>{ return new ASTIf(e, b); }
  | <WHILE> e = decl() <DO> b = decl() <END> { return new ASTWhile(e, b); }
  | <LBRAC> e = decl() {pair = new ASTPair(e);}
        (<COMMA> b = decl() {((ASTPair)pair).addValue(b);} )* <RBRAC> { return pair; }
  | <MATCH> id = <ID> { match = new ASTMatch(id.image); }
        <WITH> d = <ID> { ((ASTMatch)match).addValue(d.image); }
        (<COMMA> x = <ID> {{ ((ASTMatch)match).addValue(x.image); }})*
        <ARROW> e = decl() { ((ASTMatch)match).addBody(e); } { return match; }
  | <FIRST> e = decl() { return new ASTFirst(e); }
  | <SECOND> e = decl() { return new ASTSecond(e); }
  | <LAST> e = decl() { return new ASTLast(e); }
  | <CONCAT> <LPAR> e = decl() <COMMA> b = decl() <RPAR> { return new ASTConcat(e, b); }
  | <SPLIT> <LPAR> e = decl() <COMMA> b = decl() <RPAR> { return new ASTSplit(e, b); }
  | <PRINTLN> <LPAR> e = decl() <RPAR> { return new ASTPrintln(e);}
  | <PRINT> <LPAR> e = decl() <RPAR> { return new ASTPrint(e);}
  |  e = assignment() { return e; }
}

Exp assignment() :
{ Exp e, v; }
{
  e = logic() ( <ASSIGN> v = assignment() { return new ASTAssign(e, v); } )?
     { return e; }
}

Exp logic():
{ Exp e1,e2; }
{
  e1 = eqOp() ( <AND> e2=eqOp() { e1 = new ASTAnd(e1,e2); }
  | <OR> e2=eqOp() { e1 = new ASTOr(e1,e2); } )*
     { return e1; }
}

Exp eqOp():
{ Exp e1,e2; }
{
  e1 = boolOp() ( <EQ> e2=boolOp() { e1 = new ASTEq(e1,e2); }
  | <NEQ> e2=boolOp() {e1 = new ASTNEq(e1, e2); } )*
      { return e1; }
}

Exp boolOp():
{ Exp e1,e2; }
{
  e1 = Expr() ( <GT> e2=Expr() { e1 = new ASTGr(e1,e2); }
  | <LT> e2=Expr() { e1 = new ASTLt(e1,e2); }
  | <GTOEQ> e2=Expr() { e1 = new ASTGrOrEq(e1,e2); }
  | <LTOEQ> e2=Expr() { e1 = new ASTLTOrEq(e1,e2); } )*
      { return e1; }
}

Exp Expr() :
{ Exp e1, e2; }
{
  e1 = Term()
    [<MINUS> e2 = Expr() { e1 = new ASTSub(e1,e2); }
  | <PLUS> e2 = Expr() { e1 = new ASTAdd(e1,e2); } ]
    { return e1; }
}

Exp Term() :
{ Exp e1, e2; }
{
  e1 = fcall()
    [ <DIV> e2 = Term() { e1 = new ASTDiv(e1,e2); }
  | <TIMES> e2 = Term() { e1 = new ASTMult(e1,e2); } ]
    { return e1; }
}

Exp Fact() :
{ Token x; Exp e;}
{
  x = <Num>
    { return new ASTInt(Integer.parseInt(x.image)); }
 | x = <STRING> { return new ASTString(x.image); }
 | <TRUE> { return new ASTBool(true); }
 | <FALSE> { return new ASTBool(false); }
 | <MINUS> e = Fact() { return new ASTSub(new ASTInt(0),e); }
 | <NEG> e = Fact() { return new ASTNeg(e); }
 | <LPAR> e = logic() <RPAR> { return e; }
 | <UNIT> { return new ASTBool(true); }
 | <NEW> <LPAR> e = logic() <RPAR> { return new ASTNew(e); } // Handling reference creation
 | <DER> e = Fact() { return new ASTDeref(e); } // Handling dereference
 | x = <ID> { return new ASTIdentifier(x.image); }
}

Exp fcall():
{ Exp e; }
 {
   e=Fact() ( e=gets(e) )*
 { return e; }
}

Exp gets(Exp exp):
{ Exp e1,e2; }
 {
  <LPAR>{ e1 = new ASTCall(exp); }
  ( e2=control_flow() {((ASTCall)e1).addArg(e2); }
  ( ","e2=control_flow(){((ASTCall)e1).addArg(e2);})* )? <RPAR>
    { return e1; }
}
